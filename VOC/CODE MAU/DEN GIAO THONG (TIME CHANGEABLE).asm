
;DEN GIAO THONG 




$MOD52






RTCW EQU 0D0H       ;BYTE DIA CHI GUI TOI DS1307 KHI GHI
RTCR EQU 0D1H       ;BYTE DIA CHI GUI TOI DS1307 KHI DOC
SCL BIT P3.1        ;CHAN SCL CUA DS1307
SDA BIT P3.0        ;CHAN SDA CUA DS1307
KEYTIME EQU P3.6    ;BIT XAC DINH XEM CO AN NUT THAY DOI THOI GIAN KHONG
KEYLED EQU P3.7
DAY EQU 03H
DATE EQU 04H
MONTH EQU 05H
YEAR EQU 06H
CONTROL EQU 07H
RED_NORMAL EQU 08H
RED_PEAK EQU 0AH
TIME1_PRE EQU 0CH
TIME1_POST EQU 0EH
TIME2_PRE EQU 10H
TIME2_POST EQU 12H
SAT1_PRE EQU 14H
SAT1_POST EQU 16H
SAT2_PRE EQU 18H
SAT2_POST EQU 1AH
SUN1_PRE EQU 1CH
SUN1_POST EQU 1EH
SUN2_PRE EQU 20H
SUN2_POST EQU 22H
LASTREAD EQU 20H    ;BIT KIEM TRA XEM CO PHAI LA BIT CUOI CUNG DUOC DOC TU DS1307 KHONG
MODC EQU 21H        ;MODE CONTROL: BIT KIEM TRA XEM CO PHAI DOI SANG CHE DO KHAC CUA DEN GIAO THONG KHONG
                    ;NEU MODC=0:KHONG CAN CHUYEN
                    ;NEU MODC=1:CHUYEN DEN CHE DO LUU TRONG MODD

MOD3C EQU 22H       ;BIT LUA CHON DEN DO NAO SANG O CHE DO DIEU KHIEN BANG TAY
CLOCK_UPDATE EQU 23H        ;BIT XAC DINH XEM CO CAN CAP NHAT THOI GIAN KHONG
TIME1_PRE_UPDATE EQU 24H
TIME1_POST_UPDATE EQU 25H
TIME2_PRE_UPDATE EQU 26H
TIME2_POST_UPDATE EQU 27H
SAT1_PRE_UPDATE EQU 28H
SAT1_POST_UPDATE EQU 29H
SAT2_PRE_UPDATE EQU 30H
SAT2_POST_UPDATE EQU 31H
SUN1_PRE_UPDATE EQU 32H
SUN1_POST_UPDATE EQU 33H
SUN2_PRE_UPDATE EQU 34H
SUN2_POST_UPDATE EQU 35H
DAY_UPDATE EQU 36H
MONTH_UPDATE EQU 37H
BLINK EQU 38H       ;NEU BLINK=1 SE VAO CHE DO NHAP NHAY
BLINKTIME EQU 39H     
BLINKLED EQU 40H
BLINKSEC EQU 41H
BLINKMIN EQU 42H
BLINKHOUR EQU 43H
ONOFF EQU 44H       ;BIT XAC DINH LA BAT HAY TAT LED TRONG CHE DO NHAP NHAY
IN_MOD4 EQU 45H     ;BIT CHO BIET CO DANG O MOD4 KHONG
NO_PEAK EQU 46H
ONE_PEAK EQU 47H
TWO_PEAK EQU 48H
HOUR1_EQUAL EQU 49H
HOUR2_EQUAL EQU 50H
MODD EQU 30H        ;MODE DESTINATION: CHE DO CAN PHAI CHUYEN DEN
RED1 EQU 31H        ;CAC O NHO GIU THOI GIAN DEN XANH VA DO CUA 2 COT DEN 
GREEN1 EQU 32H
RED2 EQU 33H
GREEN2 EQU 34H
RED1_PEAK EQU 35H
GREEN1_PEAK EQU 36H
RED2_PEAK EQU 37H
GREEN2_PEAK EQU 38H
RED1_TEMP EQU 39H  
RED2_TEMP EQU 40H
RED1_PEAK_TEMP EQU 41H
RED2_PEAK_TEMP EQU 42H
RED1_LOOP EQU 43H
RED2_LOOP EQU 44H
HOUR1_PRE EQU 45H
MIN1_PRE EQU 46H
HOUR1_POST EQU 47H
MIN1_POST EQU 48H
HOUR2_PRE EQU 49H
MIN2_PRE EQU 50H
HOUR2_POST EQU 51H
MIN2_POST EQU 52H
COUNT_TIME EQU 53H        ;CAC GIA TRI TRONG VONG LAP O CHE DO NHAP NHAY
DAY_P EQU 54H 

    ORG 0000H
    LJMP MAIN
    ORG 0003H       ;NGAT NGOAI 0
    LJMP EX0ISR
    ORG 000BH       ;NGAT DO BO DINH THOI 0
    LJMP T0ISR
    ORG 0013H       ;NGAT NGOAI 1
    LJMP EX1ISR
    ORG 001BH       ;NGAT DO BO DINH THOI 1
    LJMP T1ISR
    ORG 0030H

MAIN:
    MOV DPTR,#LED7
    SETB EA
    MOV IE,#10001111B   ;CHO PHEP 4 NGAT O TREN
    SETB IT0            ;NGAT NGOAI 0 VA NGAT NGOAI 1 KICH KHOI CANH
    SETB IT1
    MOV TMOD,#01100110B  ;BO DINH THOI 0 VA 1 CO CHUC NANG DEM VA...
                        ;...HOAT DONG O CHE DO NAP LAI 8 BIT
    MOV TH1,#0FFH       ;GIA TRI NAP LAI CHO BO DINH THOI 0 VA 1...
    MOV TL1,#0FFH       ;...SAO CHO MOI KHI CO XUNG VAO, SE GAY RA TRAN VA TAO RA NGAT
    MOV TH0,#0FFH
    MOV TL0,#0FFH
    MOV COUNT_TIME,#22  ;CAC GIA TRI BAN DAU
    CLR BLINK
    CLR BLINKTIME
    CLR BLINKLED
    CLR BLINKSEC
    CLR BLINKMIN
    CLR BLINKHOUR
    SETB ONOFF
    CLR CLOCK_UPDATE
    CLR TIME1_PRE_UPDATE
    CLR TIME1_POST_UPDATE
    CLR TIME2_PRE_UPDATE
    CLR TIME2_POST_UPDATE
    CLR MODC
    CLR MOD3C
    CLR IN_MOD4
    
    SETB TR0
    SETB TR1
    ;LCALL SETCLOCK      ;THIET LAP THOI GIAN VA CHE DO CHO DS1307
        ;TRONG THUC TE, VIEC THIET LAP THOI GIAN THUC CHO DS1307 CHI CAN LAM MOT LAN...
        ;...LUC BAN DAU. SAU DO CHI CAN LAP TRINH CHO 8051 DOC GIA TRI NAY...
        ;...VA THUC HIEN CHUONG TRINH DIEU KHIEN
        ;TUY NHIEN, O DAY EM VAN VIET VAO HAM SETCLOCK
    LCALL READCLOCK     ;DOC THOI GIAN TU DS1307
        ;SAU KHI DOC THOI GIAN TU DS1307, EM LUU CAC GIA TRI GIO PHUT GIAY...
        ;... VAO BA THANH GHI R3, R4, R5. VIEC CAP NHAT GIA TRI CAC THANH GHI NAY...
        ;... DUOC THUC HIEN MOI KHI CO MOT NGAT DO CHAN SQW TU DS1307 GUI DEN
        ;... EM LAP TRINH DE SONG VUONG RA TU CHAN SQW CO TAN SO 1HZ
    LCALL CHECKMODE     ;THIET LAP CHE DO CUA DEN DUNG VOI THOI GIAN THUC

    ;________________________________________________________________________________
     ;MACRO SO SANH GIA TRI TRONG THANH GHI REG VOI GIA TRI SO NUM 
            ;NEU REG < NUM, NHAY DEN LABEL1
            ;NEU REG >= NUM, NHAY DEN LABEL2
    CMP MACRO REG,NUM,LABEL1,LABEL2
    CJNE REG,#NUM,$+3
    JC LABEL1
    AJMP LABEL2
    ENDM

    ;MACRO SO SANH GIA TRI TRONG THANH GHI A VOI GIA TRI CHUA TRONG O NHO MEM 
            ;NEU A < MEM, NHAY DEN LABEL1
            ;NEU A >= MEM, NHAY DEN LABEL2
    CMPA MACRO MEM,LABEL1,LABEL2
    CJNE A,MEM,$+3
    JC LABEL1
    AJMP LABEL2
    ENDM
        
    ;MACRO SO SANH GIA TRI TRONG THANH GHI A VOI GIA TRI...
    ;... TRONG O NHO MEM KHI DA BIET A >= MEM
            ;NEU A = MEM, NHAY DEN LABEL1
            ;NEU A > MEM, NHAY DEN LABEL2
    CMPE MACRO MEM,LABEL1,LABEL2
    SETB C
    SUBB A,MEM
    JC LABEL1
    AJMP LABEL2
    ENDM

    ;MACRO DUNG DE LUU GIA TRI DA THAY DOI CUA CAC THOI GIAN HEN GIO
SAVE MACRO NUM,L1,BIT1,L2,ADD1,L3
    CJNE R3,#NUM,L1
    JNB BIT1,L2
    WRITE2 ADD1
    CLR BIT1
    LJMP L3
ENDM

    ;MACRO GUI NOI DUNG MOT BYTE DEN DS1307
SENDB MACRO VAL1  ;VAL1 LA NOI DUNG CAN GUI
    MOV A,#VAL1
    LCALL SEND
ENDM

    ;MACRO DOC NOI DUNG MOT BYTE TU DS1307 VA LUU VAO THANH GHI REG
READB MACRO REG  
    LCALL READ
    MOV REG,A
ENDM

    ;MACRO GHI 2 GIA TRI TU 2 THANH GHI R4,R5 VAO 2 THANH GHI RAM CUA DS1307
    ;DIA CHI BAT DAU GHI LA ADDRESS
WRITE2 MACRO ADDRESS
    LCALL START
    SENDB RTCW
    SENDB ADDRESS
    MOV A,R4
    LCALL SEND
    MOV A,R5
    LCALL SEND
    LCALL STOP
ENDM

    ;MACRO DOC 2 GIA TRI TU 2 THANH GHI RAM CUA DS1307 VAO 2 THANH GHI R4 VA R5
    ;DIA CHI BAT DAU DOC LA ADDRESS
READ2 MACRO ADDRESS
    CLR LASTREAD
    LCALL START
    SENDB RTCW
    SENDB ADDRESS
    LCALL STOP
    LCALL START
    SENDB RTCR
    READB R4
    SETB LASTREAD
    READB R5
    LCALL STOP
ENDM

    ;MACRO CHUYEN GIA TRI BCD TRONG THANH GHI REG (CO GIA TRI LAY TU DS1307)...
    ;... THANH GIA TRI THAP PHAN VA LUU LAI TRONG REG
BCD_RTC_DEC MACRO REG
    MOV A,REG
    ANL A,#0FH
    MOV R7,A        ;R7 LA THANH GHI TAM THOI
    MOV A,REG
    ANL A,#0F0H
    SWAP A
    MOV B,#10
    MUL AB
    ADD A,R7
    MOV REG,A
ENDM

    ;MACRO DOC 2 GIA TRI TU 2 THANH GHI CUA DS1307, CHUYEN TU DANG BCD..
    ;... SANG DANG THAP PHAN VA LUU TRONG R4 VA R5
    ;DIA CHI THANH GHI DAU TIEN DUOC DOC LA ADDRESS
READ2_BCD MACRO ADDRESS
    CLR LASTREAD
    LCALL START
    SENDB RTCW
    SENDB ADDRESS   

    LCALL START
    SENDB RTCR
    READB R4
    SETB LASTREAD
    READB R5 
    LCALL STOP
    BCD_RTC_DEC R5
    BCD_RTC_DEC R4
ENDM

    ;MACRO CHUYEN TU GIA TRI THAP PHAN TRONG THANH GHI REG...
    ;...THANH GHI TRI BCD VA GHI VAO DS1307
DEC_BCD_RTC MACRO REG
    MOV A,REG
    LCALL BCD
    SWAP A
    ORL A,B
    LCALL SEND
ENDM

    ;MACRO GHI 2 GIA TRI THAP PHAN TU 2 THANH GHI R4 VA R5 VAO DS1307...
    ;... DUOI DANG BCD. DIA CHI BAT DAU GHI LA ADDRESS
WRITE2_BCD MACRO ADDRESS
    LCALL START
    SENDB RTCW
    SENDB ADDRESS 
    DEC_BCD_RTC R4        ;THAY DOI GIAY
    DEC_BCD_RTC R5
    LCALL STOP
ENDM

    ;MACRO DUNG DE CHUYEN GIUA CAC TRANG THAI KHI DANG O CHE DO DIEU CHINH THOI GIAN HEN GIO
BUTTON MACRO NUM1,L1,BIT1,L2,ADD1,NUM2,ADD2,L3
    CJNE R3,#NUM1,L1
    JNB BIT1,L2
    WRITE2 ADD1
    CLR BIT1
    L2: MOV R3,#NUM2
            READ2 ADD2
            LJMP L3
ENDM


;MACRO PHU CHO MACRO BITUPDATE
BIT_UP MACRO NUM,LABEL1,BIT1,LABEL2
    CJNE R3,#NUM,LABEL1
    SETB BIT1
    LJMP LABEL2
ENDM

    ;MACRO XAC DINH XEM TRANG THAI NAO DA THAY DOI
    ;... VA DO DO BIET DUOC BIT NAO CAN SET LEN 1
BITUPDATE MACRO L1,LABEL_END,L2,L3,L4,L5,L6,L7,L8,L9,L10,L11,L12,L13
    BIT_UP 22,L1,MONTH_UPDATE,LABEL_END
    L1: BIT_UP 21,L2,DAY_UPDATE,LABEL_END
    L2: BIT_UP 14,L3,TIME2_POST_UPDATE,LABEL_END
    L3: BIT_UP 13,L4,TIME2_PRE_UPDATE,LABEL_END
    L4: BIT_UP 12,L5,TIME1_POST_UPDATE,LABEL_END
    L5: BIT_UP 11,L6,TIME1_PRE_UPDATE,LABEL_END
    L6: BIT_UP 71,L7,SAT1_PRE_UPDATE,LABEL_END
    L7: BIT_UP 72,L8,SAT1_POST_UPDATE,LABEL_END
    L8: BIT_UP 73,L9,SAT2_PRE_UPDATE,LABEL_END
    L9: BIT_UP 74,L10,SAT2_POST_UPDATE,LABEL_END
    L10: BIT_UP 81,L11,SUN1_PRE_UPDATE,LABEL_END
    L11: BIT_UP 82,L12,SUN1_POST_UPDATE,LABEL_END
    L12: BIT_UP 83,L13,SUN2_PRE_UPDATE,LABEL_END
    L13: BIT_UP 84,LABEL_END,SUN2_POST_UPDATE,LABEL_END
ENDM

;MACRO PHU CHO MACRO NUM_OF_DAY
WHICH_MONTH MACRO NUM,LABEL1,LABEL2
    CJNE R0,#NUM,LABEL1
    LJMP LABEL2
ENDM

    ;MACRO DUNG DE XAC DINH THANG TRONG NAM...
    ;... DE BIET THANG DO CO BAO NHIEU NGAY MA TANG (GIAM) CHO THICH HOP
HOW_MANY_DAY MACRO M3,_31DAY,M5,M7,M8,M10,M12,M4,M6,_30DAY,M9,M11,M2,M2_28DAY,M2_29DAY
    WHICH_MONTH 1,M3,_31DAY
    M3: WHICH_MONTH 3,M5,_31DAY
    M5: WHICH_MONTH 5,M7,_31DAY
    M7: WHICH_MONTH 7,M8,_31DAY
    M8: WHICH_MONTH 8,M10,_31DAY
    M10: WHICH_MONTH 10,M12,_31DAY
    M12: WHICH_MONTH 12,M4,_31DAY
    M4: WHICH_MONTH 4,M6,_30DAY
    M6: WHICH_MONTH 6,M9,_30DAY
    M9: WHICH_MONTH 9,M11,_30DAY
    M11: WHICH_MONTH 11,M2,_30DAY
    M2: MOV A,R7
            MOV B,#4
            DIV AB
            MOV A,B
            CJNE A,#0,M2_28DAY
            LJMP M2_29DAY
ENDM

    ;MACRO DOC GIA TRI CUA CAC KHOANG THOI GIAN CAO DIEM 
READ_PEAK MACRO ADDRESS
    CLR LASTREAD
    LCALL START
    SENDB RTCW
    SENDB ADDRESS       ;GIA TRI BAN DAU DUOC DOC
                        ;TRONG CHUONG TRINH, TUY VAO THU MAY TRONG TUAN MA DOC O CAC VI TRI THICH HOP
    LCALL START
    SENDB RTCR
    READB HOUR1_PRE
    READB MIN1_PRE
    READB HOUR1_POST
    READB MIN1_POST
    READB HOUR2_PRE
    READB MIN2_PRE
    READB HOUR2_POST
    SETB LASTREAD
    READB MIN2_POST
    LCALL STOP
ENDM


        ;MACRO HIEN THI DEN GIAO THONG CHO TUNG TRANG THAI
        ;O MOI MOT TRANG THAI SE CO MOT TO HOP CAC DEN SANG VA TOI
        ;LEDVALUE LA GIA TRI NHI PHAN CUA 8 LED O CAC CHAN P1.0 -> P1.7
        ;BIT1 LA BIT CHO DEN DO VA XANH CHO NGUOI DI BO CUA COT DEN 2

    TRAFFIC MACRO LEDVALUE,BIT1,LABEL1
    CJNE R2,#2,$+5      ;NEU LA CHE DO BAN DEM HOAC DIEU KHIEN BANG TAY THI SANG...
                        ;...CHE DO DO
    LJMP MOD2
    CJNE R2,#3,$+5
    LJMP MOD3
    MOV P1,#LEDVALUE    ;HIEN THI CAC DEN P1.0 -> P1.7
    CLR BIT1            ;HIEN THI DEN XANH CHO NGUOI DI BO
    LCALL DELAY
    MOV P1,#0FFH        ;TAT CAC DEN
    SETB BIT1
    LCALL TESTKEY
    LCALL SHOWLED       ;HIEN THI CAC LED 7 DEM NGUOC
    LCALL SHOWTIME      ;HIEN THI THOI GIAN DONG HO
    LJMP LABEL1
    ENDM
    ;__________________________________________________________________
    ;BAT DAU VAO VIEC HIEN THI DEN GIAO THONG CHO TUNG TRANG THAI     
MOD01:           
    JNB MODC,M1         ;NEU MODC=1 THI PHAI CHUYEN DEN CHE DO LUU TRONG MODD
    MOV R2,MODD
    CLR MODC
    ;SO SANH CHE DO DE CHUYEN SANG DUNG CHE DO
    ;R2 LA THANH GHI LUU CHE DO
    ;R2=0:CHE DO BINH THUONG
    ;R2=1:CHE DO GIO CAO DIEM
    ;R2=2:CHE DO BAN DEM
    ;R2=3:CHE DO DIEU KHIEN BANG TAY
    ;R2=4:CHE DO MA THOI GIAN CUA CAC COT DEN DUOC THIET LAP BANG CAC NUT BAM
    ;CHE DO 4 CHI TON TAI DEN KHI NAO CO SU CHUYEN THOI GIAN DO HEN GIO...
    ;...HOAC KHI RESET LAI MACH
 M1:
    CJNE R2,#0,M2       
    MOV RED1_LOOP,RED1
    MOV RED2_LOOP,RED2
    LJMP M6
 M2:CJNE R2,#1,M3
    MOV RED1_LOOP,RED1_PEAK
    MOV RED2_LOOP,RED2_PEAK
    LJMP M6
 M3:CJNE R2,#2,M4
    LJMP MOD2
 M4:CJNE R2,#3,M5
    LJMP MOD3
 M5:MOV RED1_LOOP,RED1_TEMP
    MOV RED2_LOOP,RED2_TEMP
 M6:MOV A,RED1_LOOP      ;TINH GIA TRI CUA 2 DEN XANH
    CLR C
    SUBB A,#6
    MOV GREEN2,A
    MOV A,RED2_LOOP
    CLR C
    SUBB A,#6
    MOV GREEN1,A
    MOV A,RED1_LOOP
    CLR C
    SUBB A,#2
    MOV 7FH,A       
    MOV R1,#3            ;R1 LUU GIA TRI DEM NGUOC CHO COT DEN 2
    MOV R6,RED1_LOOP     ;R6 LUU GIA TRI DEM NGUOC CHO COT DEN 1
LOOP1:                   ;TRANG THAI 1
    MOV A,R6
    CJNE A,7FH,$+3
    JNC LAP1
    MOV R1,GREEN2   ;GIA TRI DEM NGUOC MOI CHO COT DEN 2        
LOOP2:              ;TRANG THAI 2....
    CMP R6,11,LOOP3,LAP2
LOOP3:              ;CO TAT CA 8 TRANG THAI TUONG UNG VOI 8 LOOP
    CMP R6,7,LOOP4,LAP3
LOOP4:
    CMP R6,4,LOOP4_1,LAP4
    LOOP4_1: MOV R1,RED2_LOOP      ;GIA TRI DEM NGUOC MOI CHO COT DEN 2
LOOP5:
    CMP R6,1,LOOP5_1,LAP5
    LOOP5_1: MOV R6,GREEN1         ;GIA TRI DEM NGUOC MOI CHO COT DEN 1
LOOP6:      
    CMP R6,8,LOOP7,LAP6
LOOP7:
    CMP R6,4,LOOP8,LAP7
LOOP8:
    CMP R6,1,MOD01_1,LAP8
    MOD01_1: LJMP MOD01

LAP1: TRAFFIC 11001110B,P0.5,LOOP1
LAP2: TRAFFIC 01101110B,P0.5,LOOP2
LAP3: TRAFFIC 01110110B,P0.5,LOOP3
LAP4: TRAFFIC 10110110B,P0.5,LOOP4
LAP5: TRAFFIC 11010110B,P0.6,LOOP5
LAP6: TRAFFIC 11010011B,P0.6,LOOP6
LAP7: TRAFFIC 11010011B,P0.5,LOOP7
LAP8: TRAFFIC 11010101B,P0.5,LOOP8
    ;__________________________________________________________

MOD3:
    ;CHI CO DEN DO VA XANH HOAT DONG (DIEU CHINH BANG TAY)
    ;KIEM TRA MOD3C=0 HAY MOD3C=1 DE BIET COT DEN NAO DO, COT DEN NAO XANH
MOD3_1:
    JB MOD3C,MOD3_J
    CLR P1.0                ;MOD3C=0 THI DEN DO COT DEN 1 VA DEN XANH COT DEN 2 SANG
    CLR P1.7
    LCALL DELAY
    SETB P1.0
    SETB P1.7
    LCALL TESTKEY
    LCALL SHOWTIME
    LJMP MOD3_1
MOD3_J: 
    JNB MOD3C,MOD3_1
    CLR P1.5                ;MOD3C=1 THI DEN DO COT DEN 2 VA DEN XANH COT DEN 1 SANG
    CLR P1.2
    LCALL DELAY
    SETB P1.5
    SETB P1.2
    LCALL TESTKEY
    LCALL SHOWTIME
    LJMP MOD3_J
RET
    ;________________________________________________________

MOD2:
       ;TRONG MOD 2, CHI CO 2 DEN VANG LA NHAP NHAY VOI CHU KI 2s
       ;DEN CUA DONG HO VAN BAT VA VAN CHAY
    MOV 21H,#125      ;125x8=1000ms=1s 
  WAIT1:
    CJNE R2,#2,JMP3         ;KIEM TRA NEU DUNG LA MOD2 THI MOI THUC HIEN
    MOV P1,#10111101B   ;2 DEN VANG DEU SANG
    LCALL DELAY         ;1ms
    MOV P1,#0FFH
    LCALL DELAY         ;1ms
    LCALL TESTKEY
    JNB MODC,WAIT1_1    
    MOV R2,MODD
  WAIT1_1: 
    LCALL SHOWTIME      ;6ms
    DJNZ 21H,WAIT1

    MOV 21H,#125        ;125x8ms=1s
    MOV P1,#0FFH        ;TAT CA CAC DEN DEU TAT
  WAIT2:
    CJNE R2,#2,JMP3
    LCALL DELAY          ;1ms
    LCALL DELAY          ;1ms
    LCALL TESTKEY
    JNB MODC,WAIT1_2    
    MOV R2,MODD
  WAIT1_2: 
    LCALL SHOWTIME       ;6ms
    DJNZ 21H,WAIT2
    LJMP MOD2
  JMP3: CJNE R2,#3,J2
        AJMP MOD3
  J2: LJMP MOD01
RET
    ;___________________________________________________________

CHECKMODE:
    ;CHON CHE DO CHO DEN TUY VAO THOI GIAN THUC
    ;MOI KHI RESET LAI HAY CO THAY DOI THOI GIAN...
    ;... BANG NUT BAM, VDK PHAI CHON CHE DO CHO PHU HOP VOI THOI GIAN THUC
    ;CU MOI 1 PHUT CUNG THUC HIEN VIEC CHON CHE DO
    ;CACH SO SANH: SO SANH THEO GIO TU CAO XUONG THAP, NEU O TRONG...
    ;...CHE DO NAO THI CHUYEN SANG CHE DO DO
    CLR NO_PEAK
    CLR ONE_PEAK
    CLR TWO_PEAK
    CLR HOUR1_EQUAL
    CLR HOUR2_EQUAL
    LCALL READCLOCK
    MOV A,DAY_P
    CJNE A,#1,CHECK_SAT
    READ_PEAK SUN1_PRE
    LJMP CH_START
    CHECK_SAT: CJNE A,#7,CHECK_DAY
               READ_PEAK SAT1_PRE
               LJMP CH_START
    CHECK_DAY: READ_PEAK TIME1_PRE
    CH_START: MOV R7,HOUR1_PRE
              CMP R7,6,C0,C00
    C0: SETB NO_PEAK
        AJMP CHECK_START
    C00: MOV A,HOUR1_POST
         CMPA HOUR1_PRE,C0,C00_0    
    C00_0: CMPE HOUR1_PRE,EQUAL1,C01
    EQUAL1: MOV A,MIN1_POST
            CMPE MIN1_PRE,C0,EQUAL1_0
    EQUAL1_0: SETB HOUR1_EQUAL
    C01: SETB ONE_PEAK
         MOV A,HOUR2_PRE
         CMPE HOUR1_POST,CHECK_START,C02
    C02: MOV A,HOUR2_POST
         CMPA HOUR2_PRE,CHECK_START,C02_0
    C02_0: CMPE HOUR2_PRE,EQUAL2,C03
    EQUAL2: MOV A,MIN2_POST
            CMPE MIN2_PRE,CHECK_START,EQUAL2_0
    EQUAL2_0: SETB HOUR2_EQUAL
    C03: SETB TWO_PEAK
         CLR ONE_PEAK

    CHECK_START:            ;BAT DAU KIEM TRA CHE DO 
    JNB NO_PEAK,ONE_DURATION                    ;KHONG CO KHOANG THOI GIAN CAO DIEM NAO
    CMP R3,6,SELMOD2,SELECTMOD0
    
    ONE_DURATION: JNB ONE_PEAK,TWO_DURATION     ;CO 1 KHOANG THOI GIAN CAO DIEM
                  MOV A,R3
                  CMPA HOUR1_POST,C1,C2
    C2: CMPE HOUR1_POST,CMP_MIN1,SELECTMOD0
    CMP_MIN1: JB HOUR1_EQUAL,CMP_MIN1_1
              MOV A,R4
              CMPA MIN1_POST,SELMOD1,SELECTMOD0
    CMP_MIN1_1: MOV A,R4
                CMPA MIN1_POST,CMP_MIN1_1_0,SELECTMOD0
    CMP_MIN1_1_0: CMPA MIN1_PRE,SELECTMOD0,SELECTMOD1

    SELMOD1: AJMP SELECTMOD1
    SELMOD2: AJMP SELECTMOD2

    C1: JB HOUR1_EQUAL,C4
        CMPA HOUR1_PRE,C4,C5
    C5: CMPE HOUR1_PRE,CMP_MIN2,SELECTMOD1
    CMP_MIN2: MOV A,R4
              CMPA MIN1_PRE,SELECTMOD0,SELECTMOD1
    C4: CMP R3,6,SELECTMOD2,SELECTMOD0

    TWO_DURATION: MOV A,R3                      ;CO 2 KHOANG THOI GIAN CAO DIEM
                  CMPA HOUR2_POST,C6,C7
    C7: CMPE HOUR2_POST,CMP_MIN3,SELECTMOD0
    CMP_MIN3: JB HOUR2_EQUAL,CMP_MIN3_1
              MOV A,R4
              CMPA MIN2_POST,SELECTMOD1,SELECTMOD0
    CMP_MIN3_1: MOV A,R4
                CMPA MIN2_POST,CMP_MIN3_1_0,SELECTMOD0
    CMP_MIN3_1_0: CMPA MIN2_PRE,SELECTMOD0,SELECTMOD1

    C6: JB HOUR2_EQUAL,C8
        CMPA HOUR2_PRE,C9,C10
    C10: CMPE HOUR2_PRE,CMP_MIN4,SELECTMOD1
    CMP_MIN4: MOV A,R4
              CMPA MIN2_PRE,SELECTMOD0,SELECTMOD1
    C9: CMPA HOUR1_POST,C11,C12
    SELECTMOD0: SETB MODC
                MOV MODD,#0  
                LJMP CHECK_END
    SELECTMOD1: SETB MODC
                MOV MODD,#1  
                LJMP CHECK_END
    SELECTMOD2: MOV R2,#2   
                CLR MODC
                LJMP CHECK_END

    C12: CMPE HOUR1_POST,CMP_MIN5,SELECTMOD0
    CMP_MIN5: JB HOUR1_EQUAL,CMP_MIN5_1
              MOV A,R4
              CMPA MIN1_POST,SELECTMOD1,SELECTMOD0
    CMP_MIN5_1: MOV A,R4
                CMPA MIN1_POST,CMP_MIN5_1_0,SELECTMOD0
    CMP_MIN5_1_0: CMPA MIN1_PRE,SELECTMOD0,SELECTMOD1
    C4_T: AJMP C4
    SELMOD0: AJMP SELECTMOD0

    C11: JB HOUR1_EQUAL,C4_T
         CMPA HOUR1_PRE,C4_T,C11_0
    C11_0: CMPE HOUR1_PRE,CMP_MIN6,SELECTMOD1
    CMP_MIN6: MOV A,R4
              CMPA MIN1_PRE,SELECTMOD0,SELECTMOD1

    C8: CMPA HOUR1_POST,C13,C14
    C14: CMPE HOUR1_POST,C14_0,SELECTMOD0
    C14_0: JB HOUR1_EQUAL,CMP_MIN7
           MOV A,R4
           CMPA MIN1_POST,SELECTMOD1,SELECTMOD0
    CMP_MIN7: MOV A,R4
              CMPA MIN1_POST,CMP_MIN7_0,SELECTMOD0
    CMP_MIN7_0: CMPA MIN1_PRE,SELECTMOD0,SELECTMOD1
    C13: JB HOUR1_EQUAL,C4_T
         CMPA HOUR1_PRE,C4_T,C13_0
    C13_0: CMPE HOUR1_PRE,CMP_MIN8,SELECTMOD1
    CMP_MIN8: MOV A,R4
              CMPA MIN1_PRE,SELMOD0,SELECTMOD1
CHECK_END: RET   
    ;_______________________________________________________________________

TESTKEY:
        ;KIEM TRA TRANG THAI CUA 2 CONG TAC DIEU CHINH THOI GIAN DONG HO VA COT DEN
        ;NEU CA 2 CONG TAC DANG DEU DUOC AN, CHI CO CONG TAC NAO AN TRUOC MOI CO TAC DUNG
    JB BLINKLED,TEST_KEYLED            
        ;KHONG CAN TAO CAC THOI GIAN TRE VI HIEN TUONG DOI DA DUOC LOAI BO BANG PHAN CUNG
    SETB KEYTIME           ;SET CHAN KEYTIME LAM DAU VAO
                           ;KEYTIME LA CONG TAC DE VAO CHE DO THAY DOI THOI GIAN 
        ;DE KIEM TRA TRANG THAI CUA CONG TAC, TRUOC HET DOC GIA TRI CUA NO...
        ;..., SAU DO TUY VAO TRANG THAI TRUOC DO CUA CONG TAC MA DIEU KHIEN CHO THICH HOP
        ;TRANG THAI TRUOC DO DUOC XAC DINH DUA VAO...
        ;...CAC BIEN DIEU KHIEN BLINKTIME (NUT KEYTIME) VA BLINKLED (NUT KEYLED) 
    JNB KEYTIME,TEST1               
    JNB BLINKTIME,TEST_KEYLED      ;KEYTIME DANG KHONG DUOC AN VA TRUOC DO CUNG KHONG DUOC AN
    CLR BLINK                   ;KEYTIME DANG KHONG DUOC AN VA TRUOC DO DUOC AN
    CLR BLINKTIME
    CLR BLINKSEC
    CLR BLINKMIN
    CLR BLINKHOUR
    JNB CLOCK_UPDATE,TEST_KEYLED    
    LCALL CHANGECLOCK           ;NEU CO THAY DOI THOI GIAN THI MOI CAP NHAT LAI
    LCALL CHECKMODE
    CLR CLOCK_UPDATE
    LJMP T_END

    TEST1: JB BLINKTIME,T_END    ;KEYTIME DANG DUOC AN VA TRUOC DO CUNG DUOC AN
           SETB BLINK               ;KEYTIME DANG DUOC AN VA TRUOC DO KHONG DUOC AN
           SETB BLINKTIME
           SETB BLINKSEC
           LJMP T_END
    
    T_END: LJMP TEST_END
        ;KEYLED LA CONG TAC DE VAO CHE DO DIEU CHINH...
        ;...THOI GIAN CUA CAC COT DEN VA THOI GIAN HEN GIO
    TEST_KEYLED: SETB KEYLED
                 JNB KEYLED,TEST2_T
                 JNB BLINKLED,T_END
                 CJNE R3,#0,TEST_01
                 CJNE R4,#16,TE_3
                 LJMP TEST_W
    TE_3: CJNE R5,#16,TE_4
          LJMP TEST_W
    TE_4: MOV RED1_TEMP,R4
          MOV RED2_TEMP,R5
          SETB IN_MOD4
          SETB MODC
          MOV MODD,#4
          LJMP TEST_W

    TEST_01: CJNE R3,#1,TEST_02
             CJNE R4,#16,TE_5
             LJMP TEST_W
    TE_5: CJNE R5,#16,TE_6
          LJMP TEST_W
    TE_6: WRITE2 RED_NORMAL
          LJMP TEST_W
           
    TEST_02: CJNE R3,#2,TEST_11
             CJNE R4,#16,TE_7
             LJMP TEST_W
    TE_7: CJNE R5,#16,TE_8
          LJMP TEST_W
    TE_8: WRITE2 RED_PEAK
          LJMP TEST_W
    TEST2_T: LJMP TEST2

    TEST_11: SAVE 11,TEST_12,TIME1_PRE_UPDATE,TEST_W_TG1,TIME1_PRE,TEST_W
    TEST_12: SAVE 12,TEST_13,TIME1_POST_UPDATE,TEST_W_TG1,TIME1_POST,TEST_W
    TEST_13: SAVE 13,TEST_14,TIME2_PRE_UPDATE,TEST_W_TG1,TIME2_PRE,TEST_W
    TEST_W_TG1: LJMP TEST_W
    TEST_14: SAVE 14,TEST_71,TIME2_POST_UPDATE,TEST_W_TG1,TIME2_POST,TEST_W
    TEST_71: SAVE 71,TEST_72,SAT1_PRE_UPDATE,TEST_W_TG1,SAT1_PRE,TEST_W
    TEST_72: SAVE 72,TEST_73,SAT1_POST_UPDATE,TEST_W_TG,SAT1_POST,TEST_W
    TEST_73: SAVE 73,TEST_74,SAT2_PRE_UPDATE,TEST_W_TG,SAT2_PRE,TEST_W
    TEST_74: SAVE 74,TEST_81,SAT2_POST_UPDATE,TEST_W_TG,SAT2_POST,TEST_W
    TEST_W_TG: LJMP TEST_W
    TEST_81: SAVE 81,TEST_82,SUN1_PRE_UPDATE,TEST_W_TG,SUN1_PRE,TEST_W
    TEST_82: SAVE 82,TEST_83,SUN1_POST_UPDATE,TEST_W_TG,SUN1_POST,TEST_W
    TEST_83: SAVE 83,TEST_84,SUN2_PRE_UPDATE,TEST_W_TG,SUN2_PRE,TEST_W
    TEST_84: SAVE 84,TEST_21,SUN2_POST_UPDATE,TEST_W,SUN2_POST,TEST_W
    
    TEST_21: CJNE R3,#21,TEST_22
             JNB DAY_UPDATE,TEST_W
             WRITE2_BCD DAY
             CLR DAY_UPDATE
             LJMP TEST_W

    TEST_22: JNB MONTH_UPDATE,TEST_W
             WRITE2_BCD MONTH
             CLR MONTH_UPDATE

    TEST_W: LCALL READCLOCK
            CLR BLINK
            CLR BLINKLED
            CLR BLINKSEC
            CLR BLINKMIN
            CLR BLINKHOUR
            LJMP TEST_END
    
    TEST2: JB BLINKLED,TEST_END
           SETB BLINK
           SETB BLINKLED
           SETB BLINKHOUR
           LCALL CHANGECLOCK        ;LUU THOI GIAN VAO DONG HO DS1307
           MOV R3,#0
           MOV R4,#16
           MOV R5,#16
           LJMP TEST_END
TEST_END: RET

            ;MACRO HIEN THI GIA TRI TRONG 1 THANH GHI R
            ;VA HIEN LEN 2 LED 7 THANH DUOC NOI VOI BIT1 VA BIT2
    SHOWLED2 MACRO R,BIT1,BIT2
    MOV A,R
    LCALL BCD
    MOVC A,@A+DPTR
    CLR BIT1
    MOV P2,A
    LCALL DELAY
    SETB BIT1
    MOV A,B
    MOVC A,@A+DPTR
    CLR BIT2
    MOV P2,A
    LCALL DELAY
    SETB BIT2
    ENDM

            ;MACRO HIEN THI GIA TRI TRONG 1 THANH GHI R
            ;VA HIEN LEN 2 LED 7 THANH DUOC XAC DINH TU DEMUX QUA PORT0 CUA 8051
    SHOWLED1 MACRO R,VAL1,VAL2
    MOV A,R
    LCALL BCD
    MOVC A,@A+DPTR
    MOV P0,#VAL1
    MOV P2,A
    LCALL DELAY
  
    MOV A,B
    MOVC A,@A+DPTR
    MOV P0,#VAL2
    MOV P2,A
    LCALL DELAY
    MOV P0,#07FH
    ENDM
    ;_____________________________________________________________
SHOWLED:       ;HIEN THI DEN DEM NGUOC
    SHOWLED1 R6,0FBH,0FAH             ;COT DEN 1
                                     ;R6 CHUA GIA TRI CAN HIEN THI
    SHOWLED1 R1,0F9H,0F8H             ;COT DEN 2 
                                     ;R1 LUU GIA TRI CAN HIEN THI
RET
    ;___________________________________________________________
            ;MACRO HIEN THI LED TRONG CHE DO NHAP NHAY
            ;...KHI LED DANG TAT
            ;VAL1 LA O NHO CHUA CHI SO LAP LAI
            ;NUM LA CHI SO LAP LAI DUOC SET LAI KHI DA VE 0
BLINK_OFF MACRO VAL1,LABEL1,NUM
    LCALL DELAY 
    LCALL DELAY
    DJNZ VAL1,LABEL1
    CPL ONOFF
    MOV VAL1,#NUM
    LJMP LABEL1
ENDM

SHOWTIME:          
        ;HIEN THI 6 LED HIEN THI THOI GIAN
        ;TUY VAO GIA TRI CUA CAC BIT DIEU KHIEN MA CO VAO...
        ;... CHE DO NHAP NHAY HAY KHONG, VA NHAP NHAY DEN NAO
    JNB BLINK,S1
    JNB BLINKSEC,S4
    JNB ONOFF,S2
    DJNZ COUNT_TIME,S1
    CPL ONOFF
    MOV COUNT_TIME,#22
    LJMP S1
    S4: JNB BLINKMIN,S5
        JNB ONOFF,S6
        DJNZ COUNT_TIME,S1
        CPL ONOFF
        MOV COUNT_TIME,#22
        LJMP S1
    S2: BLINK_OFF COUNT_TIME,S3,22
    S6: LCALL DELAY 
        LCALL DELAY
        DJNZ COUNT_TIME,S7_1
        CPL ONOFF
        MOV COUNT_TIME,#22
        LJMP S7
    S7_1: LJMP S7
    S8: BLINK_OFF COUNT_TIME,S9,22
    S5: JNB ONOFF,S8
        DJNZ COUNT_TIME,S1
        CPL ONOFF
        MOV COUNT_TIME,#22
        LJMP S1
    S1: SHOWLED1 R5,0FDH,0FCH       ;HIEN THI GIAY
    S3: SHOWLED1 R4,0FFH,0FEH       ;HIEN THI PHUT
        SHOWLED2 R3,P0.4,P0.3       ;HIEN THI GIO
    LJMP SHOWEND
    S9: SHOWLED1 R5,0FDH,0FCH       ;HIEN THI GIAY
        SHOWLED1 R4,0FFH,0FEH       ;HIEN THI PHUT
    LJMP SHOWEND
    S7: SHOWLED1 R5,0FDH,0FCH       ;HIEN THI GIAY
        SHOWLED2 R3,P0.4,P0.3       ;HIEN THI GIO
SHOWEND: RET
    ;___________________________________________________________



START:              ;TRANG THAI BAT DAU CUA VIEC GHI/DOC VAO DS1307
    SETB SDA
    LCALL SCLHIGH
    CLR SDA
    NOP
    CLR SCL
RET

STOP:               ;TRANG THAI KET THUC CUA VIEC GHI/DOC VAO DS1307
    CLR SDA
    LCALL SCLHIGH
    SETB SDA
RET

SEND:               ;GUI 1 BYTE DEN DS1307 (BAT DAU TU BIT MSB)
    MOV 21H,#8
    LOOPS:          ;VONG LAP GUI 8 BIT DU LIEU TRONG 1 BYTE
        RLC A
        MOV SDA,C
        NOP
        LCALL SCLHIGH
        CLR SCL
        DJNZ 21H,LOOPS
    SETB SDA        ;TAO BIT ACKNOWLEDGE DO DS1307 GUI DEN SDA
    NOP
    LCALL SCLHIGH
    CLR SCL
    NOP
    CLR SDA
RET

READ:                   ;DOC 1 BYTE TU DS1307 (BAT TU TU BIT MSB)
    MOV 21H,#8
    MOV A,#0
    LOOPR:              ;VONG LAP DE DOC 8 BIT
        SETB SDA
        NOP
        LCALL SCLHIGH
        MOV C,SDA
        RLC A
        CLR SCL
        NOP
        DJNZ 21H,LOOPR
    MOV C,LASTREAD      ;LASTREAD LA BIT BAO HIEU LA BYTE CUOI CUNG DUOC DOC
                        ;NEU LASTREAD=1, SDA=1; NEU LASTREAD=0, SDA=0
                        ;DAY LA BIT NOT ACKNOWLEDGE CUA 8051 GUI DEN DS1307
    MOV SDA,C
    NOP
    LCALL SCLHIGH
    CLR SCL
    NOP
    CLR SDA
RET

        
SETCLOCK:         ;THIET LAP THOI GIAN BAN DAU CHO DS1307 (THOI GIAN THUC)
    LCALL START
    SENDB RTCW
    SENDB 00H     ;THIET LAP GIA TRI BAN DAU DUOC GHI LA THANH GHI SECONDS CUA DS1307
    SENDB 30H     ;08:29:30
    SENDB 29H
    SENDB 08H
    SENDB 02H     ;THU 2 NGAY 26/12/2005
    SENDB 26H
    SENDB 12H
    SENDB 05H
    SENDB 90H     ;THIET LAP THANH GHI DIEU KHIEN CUA DS1307
    SENDB 20      ;GIA TRI THOI GIAN 2 DEN DO CUA 2 COT DEN O CHE DO BINH THUONG
    SENDB 26
    SENDB 30
    SENDB 36
    SENDB 7
    SENDB 30
    SENDB 8
    SENDB 30
    SENDB 16
    SENDB 0
    SENDB 18
    SENDB 0
    SENDB 7
    SENDB 30
    SENDB 8
    SENDB 30
    SENDB 17
    SENDB 0
    SENDB 20
    SENDB 30
    SENDB 18
    SENDB 30
    SENDB 20
    SENDB 30
    SENDB 0
    SENDB 0
    SENDB 0
    SENDB 0
    LCALL STOP
RET 

READCLOCK:          ;DOC GIA TRI GIO PHUT GIAY TU DS1307 VA LUU VAO R3, R4, R5
    CLR LASTREAD
    LCALL START
    SENDB RTCW
    SENDB 00H       ;GIA TRI BAN DAU DUOC DOC LA O THANH GHI SECONDS CUA DS1307
    
    LCALL START
    SENDB RTCR
    READB R5        ;DOC GIAY
    READB R4        ;DOC PHUT
    READB R3        ;DOC GIO
    SETB LASTREAD
    READB DAY_P     ;DOC THU
    LCALL STOP
    BCD_RTC_DEC R5
    BCD_RTC_DEC R4
    BCD_RTC_DEC R3
RET
    
CHANGECLOCK:        ;THAY DOI THOI GIAN DONG HO
    LCALL START
    SENDB RTCW
    SENDB 00H       ;THIET LAP GIA TRI BAN DAU DUOC GHI LA THANH GHI SECONDS CUA DS1307
                    ;CAN PHAI CHUYEN DINH DANG NHI PHAN TRONG CAC THANH GHI...
                    ;... THANH DINH DANG BCD TUONG UNG CUA DONG HO THOI GIAN THUC DS1307
    DEC_BCD_RTC R5        ;THAY DOI GIAY
    DEC_BCD_RTC R4        ;THAY DOI PHUT
    DEC_BCD_RTC R3        ;THAY DOI GIO
    LCALL STOP
RET 

SCLHIGH:        ;DUA SCL LEN CAO
    SETB SCL
    NOP
    NOP
    NOP
    NOP
RET

BCD:        ;CHUYEN TU NHI PHAN SANG THAP PHAN
        MOV B,#10           ; B=10
        DIV AB              ; CHIA CHO 10
                            ; CHI CAN THUC HIEN CHIA 1 LAN DO 
                            ; CAC SO SU DUNG <=60
RET
    ;____________________________________________________________
DELAY:      ;TRE 1ms (4x250us=1ms)
    MOV 20H,#250
    WAIT: 
    NOP
    NOP
    DJNZ 20H,WAIT
RET
    ;____________________________________________________________
EX0ISR:       
      DEC R6    ;DUOC 1s LAI GIAM GIA TRI CUA 2 DEN DEM NGUOC (LUU TRONG R6 VA R1)
      DEC R1
      JB BLINKLED,INT_END
      CJNE R5,#59,INC_SEC
      MOV R5,#0
      CJNE R4,#59,INC_MIN
      MOV R4,#0
      CJNE R3,#23,INC_HOUR
      MOV R3,#0
      MOV R2,#2
      CLR MODC
      LJMP INT_END
      INC_SEC: INC R5
               LJMP INT_END
      INC_MIN: INC R4
               JB BLINKTIME,INT_END     ;KHONG XET THAY DOI CHE DO, CHI CAP NHAT THOI GIAN...
               JB IN_MOD4,INT_END       ;...KHI DANG TRONG TRANG THAI THAY DOI THOI GIAN
               LCALL CHECKMODE
               LJMP INT_END
      INC_HOUR:INC R3
               JB BLINKTIME,INT_END
               JB IN_MOD4,INT_END
               LCALL CHECKMODE
INT_END: RETI
    ;_____________________________________________________________

EX1ISR:           
        ;DAY LA NUT BAM LUA CHON CHE DO VA TRANG THAI
        ;NEU 2 CONG TAC DIEU CHINH THOI GIAN KHONG DUOC AN, NUT NAY...
        ;... CO TAC DUNG VAO CHE DO DIEU CHINH DEN BANG TAY (MOD3)
        ;NEU 2 CONG TAC DIEU CHINH THOI GIAN DUOC AN, NUT NAY...
        ;... LAM NHIEM VU CHUYEN TU DEN NAY SANG DEN KHAC DE THAY DOI
        ;... VD CHUYEN TU THAY DOI GIAY SANG THAY DOI PHUT
        ;... CUNG DUA VAO CAC BIT DIEU KHIEN DE LUA CHON CHE DO CHO THICH HOP
    JNB BLINK,TOMOD3                ;CHUYEN SANG CHE DO DIEU KHIEN DEN BANG TAY..
                                    ;..KHI 2 CONG TAC KHONG DUOC AN
    JNB BLINKSEC,EX1_2              
    CLR BLINKSEC
    SETB BLINKMIN
    LJMP EX1_END
    EX1_2: JNB BLINKMIN,EX1_3
           CLR BLINKMIN
           SETB BLINKHOUR
           LJMP EX1_END
    EX1_3: CLR BLINKHOUR
           SETB BLINKSEC
           LJMP EX1_END
    TOMOD3: MOV R2,#3
            CPL MOD3C
    EX1_END: RETI

T1ISR:      ;NUT TANG
            ;KHI 2 CONG TAC DIEU CHINH THOI GIAN KHONG DUOC AN, NUT NAY KHONG CO TAC DUNG
            ;DUA VAO CAC BIT DIEU KHIEN MA LUA CHON DUNG CAC DEN DE TANG DUNG LOAI CHI SO
            ;PHAI TINH DEN CAC TRUONG HOP DAC BIET DE TANG CHO THICH HOP...
            ;...VD CHI SO GIO DANG =23 THI PHAI CHUYEN VE 0
            ;...CHI SO THOI GIAN CUA COT DEN LA 98 THI CHUYEN VE 0
    JNB BLINK,T1_END_TG
    JNB BLINKTIME,T1_BLINKLED       
    SETB CLOCK_UPDATE
    JNB BLINKSEC,T1_MIN     
    LJMP T1_END
    T1_MIN: JNB BLINKMIN,T1_HOUR
            CJNE R4,#59,T1_MIN1
            MOV R4,#0
            LJMP T1_END
    T1_MIN1: INC R4
            LJMP T1_END
    T1_HOUR:CJNE R3,#23,T1_HOUR1
            MOV R3,#0
            LJMP T1_END
    T1_HOUR1: INC R3
              LJMP T1_END
    T1_END_TG: LJMP T1_END

    T1_BLINKLED: JB BLINKHOUR,T1_BLINKHOUR_TG
                 BITUPDATE T1_100,I0,T1_200,T1_300,T1_400,T1_500,T1_600,T1_700,T1_800,T1_900,T1_1000,T1_1100,T1_1200,T1_1300
    T1_BLINKHOUR_TG: LJMP T1_BLINKHOUR
    I0: JNB BLINKSEC,T1_BLINKMIN_TG
        CMP R3,71,I0_1,I_MIN_TG
        I_MIN_TG: LJMP I_MIN
    I0_1: CJNE R3,#22,T1_7
          CJNE R5,#99,INC_R5_TG            ;TANG CHI SO NAM
          MOV R5,#0
          LJMP T1_END
    T1_BLINKMIN_TG: LJMP T1_BLINKMIN
    INC_R5_TG: LJMP INC_R5
    T1_8_TG: LJMP T1_8
    T1_7: CJNE R3,#21,T1_8_TG
          ;TANG CHI SO NGAY
          PUSH 4
          PUSH 5
          READ2_BCD MONTH
          MOV A,R4
          MOV R0,A          ;R0 LUU GIA TRI THANG
          MOV A,R5
          MOV R7,A          ;R7 LUU GIA TRI NAM
          POP 5
          POP 4
    
    HOW_MANY_DAY T1_M3,T1_31DAY,T1_M5,T1_M7,T1_M8,T1_M10,T1_M12,T1_M4,T1_M6,T1_30DAY,T1_M9,T1_M11,T1_M2,T1_M2_28DAY,T1_M2_29DAY
    T1_30DAY: CMP R5,30,INC_R5,R5_1
    T1_31DAY: CMP R5,31,INC_R5,R5_1
    T1_M2_28DAY: CMP R5,28,INC_R5,R5_1
    T1_M2_29DAY: CMP R5,29,INC_R5,R5_1
    R5_1: MOV R5,#1
          LJMP T1_END
    T1_8: CMP R3,11,I_RED2,I_MIN
    I_RED2: CJNE R5,#98,INC_RED2        ;TANG THOI GIAN DEN DO CUA COT DEN 2
            MOV R5,#16
            LJMP T1_END
    INC_RED2: MOV A,R5
              ADD A,#2
              MOV R5,A
              LJMP T1_END
    I_MIN: CJNE R5,#59,INC_R5
           MOV R5,#0
           LJMP T1_END
    INC_R5: INC R5
            LJMP T1_END

    T1_BLINKMIN: JNB BLINKMIN,T1_BLINKHOUR
                 CMP R3,71,T1_BLINKMIN_1,I_HOUR
    T1_BLINKMIN_1: CJNE R3,#22,T1_9
                   CJNE R4,#12,INC_R4       ;TANG CHI SO THANG
                   MOV R4,#1
                   LJMP T1_END

    T1_9: CJNE R3,#21,T1_10            ;TANG CHI SO THU TRONG TUAN
           CJNE R4,#7,INC_R4
           MOV R4,#1
           LJMP T1_END

    T1_10: CMP R3,11,I_RED1,I_HOUR
    I_RED1: CJNE R4,#98,INC_RED1
            MOV R4,#16
            LJMP T1_END
    INC_RED1: MOV A,R4
              ADD A,#2
              MOV R4,A
              LJMP T1_END
    I_HOUR: CJNE R4,#23,INC_R4
            MOV R4,#0
            LJMP T1_END
    INC_R4: INC R4
            LJMP T1_END
    
    T1_BLINKHOUR: CJNE R3,#0,T1_01
                  CJNE R4,#16,T1_H3
                  LJMP T1_R1
    T1_H3: CJNE R5,#16,T1_H4
          LJMP T1_R1
    T1_H4: MOV RED1_TEMP,R4
          MOV RED2_TEMP,R5
          SETB IN_MOD4
          SETB MODC
          MOV MODD,#4
    T1_R1: READ2 RED_NORMAL
           MOV R3,#1
           LJMP T1_END

    T1_01: CJNE R3,#1,T1_02
           CJNE R4,#16,T1_H5
           LJMP T1_R2
    T1_H5: CJNE R5,#16,T1_H6
          LJMP T1_R2
    T1_H6: WRITE2 RED_NORMAL
    T1_R2: READ2 RED_PEAK
           MOV R3,#2
           LJMP T1_END
           
    T1_02: CJNE R3,#2,T1_11
           CJNE R4,#16,T1_H7
           LJMP T1_R3
    T1_H7: CJNE R5,#16,T1_H8
          LJMP T1_R3
    T1_H8: WRITE2 RED_PEAK
    T1_R3: READ2 TIME1_PRE
           MOV R3,#11
           LJMP T1_END

T1_11: BUTTON 11,T1_12,TIME1_PRE_UPDATE,LB1,TIME1_PRE,12,TIME1_POST,T1_END
T1_12: BUTTON 12,T1_13,TIME1_POST_UPDATE,LB2,TIME1_POST,13,TIME2_PRE,T1_END
T1_13: BUTTON 13,T1_14,TIME2_PRE_UPDATE,LB3,TIME2_PRE,14,TIME2_POST,T1_END
T1_14: BUTTON 14,T1_71,TIME2_POST_UPDATE,LB4,TIME2_POST,71,SAT1_PRE,T1_END
T1_71: BUTTON 71,T1_72,SAT1_PRE_UPDATE,LB5,SAT1_PRE,72,SAT1_POST,T1_END
T1_72: BUTTON 72,T1_73,SAT1_POST_UPDATE,LB6,SAT1_POST,73,SAT2_PRE,T1_END
T1_73: BUTTON 73,T1_74,SAT2_PRE_UPDATE,LB7,SAT2_PRE,74,SAT2_POST,T1_END
T1_74: BUTTON 74,T1_81,SAT2_POST_UPDATE,LB8,SAT2_POST,81,SUN1_PRE,T1_END
T1_81: BUTTON 81,T1_82,SUN1_PRE_UPDATE,LB9,SUN1_PRE,82,SUN1_POST,T1_END
T1_82: BUTTON 82,T1_83,SUN1_POST_UPDATE,LB10,SUN1_POST,83,SUN2_PRE,T1_END
T1_83: BUTTON 83,T1_84,SUN2_PRE_UPDATE,LB11,SUN2_PRE,84,SUN2_POST,T1_END

T1_84: CJNE R3,#84,T1_DAY
       JNB SUN2_POST_UPDATE,T1_84_1
       WRITE2 SUN2_POST
       CLR SUN2_POST_UPDATE
T1_84_1: MOV R3,#21
         READ2_BCD DAY
         LJMP T1_END

T1_DAY: CJNE R3,#21,T1_MONTH
        JNB DAY_UPDATE,T1_DAY_1
        WRITE2_BCD DAY
        CLR DAY_UPDATE
T1_DAY_1: MOV R3,#22
          READ2_BCD MONTH
          LJMP T1_END

T1_MONTH: JNB MONTH_UPDATE,T1_MONTH_1
          WRITE2_BCD MONTH
          CLR MONTH_UPDATE
T1_MONTH_1: MOV R3,#0
            MOV R4,#16
            MOV R5,#16
T1_END: RETI

T0ISR:      ;NUT GIAM
            ;PHAI TINH DEN CAC TRUONG HOP DAC BIET DE GIAM CHO THICH HOP...
            ;...VD CHI SO GIO DANG =0 THI PHAI CHUYEN VE 23
            ;...CHI SO THOI GIAN CUA COT DEN LA 16 THI KHONG CHO GIAM NUA
    JNB BLINK,T0_END_TG
    JNB BLINKTIME,T0_BLINKLED
    SETB CLOCK_UPDATE
    JNB BLINKSEC,T0_MIN     
    MOV R5,#0
    LJMP T0_END

    T0_MIN: JNB BLINKMIN,T0_HOUR
            CJNE R4,#0,T0_1
            MOV R4,#59
            LJMP T0_END
    T0_1:   DEC R4
            LJMP T0_END
    T0_HOUR:CJNE R3,#0,T0_HOUR_1
            MOV R3,#23
            LJMP T0_END
    T0_HOUR_1: DEC R3
               LJMP T0_END
    T0_END_TG: LJMP T0_END

    T0_BLINKLED: JB BLINKHOUR,T0_BLINKHOUR_TG
                 BITUPDATE T0_100,D0,T0_200,T0_300,T0_400,T0_500,T0_600,T0_700,T0_800,T0_900,T0_1000,T0_1100,T0_1200,T0_1300
    T0_BLINKHOUR_TG: LJMP T0_BLINKHOUR
    D0: JNB BLINKSEC,T0_BLINKMIN_TG
        CMP R3,71,D0_1,D_MIN_TG
    D_MIN_TG: LJMP D_MIN
    D0_1: CJNE R3,#22,T0_7
          CJNE R5,#0,DEC_R5_TG            ;GIAM CHI SO NAM
          MOV R5,#99
          LJMP T0_END
    T0_BLINKMIN_TG: LJMP T0_BLINKMIN
    DEC_R5_TG: LJMP DEC_R5
    T0_8_TG: LJMP T0_8
    T0_7: CJNE R3,#21,T0_8_TG           ;GIAM CHI SO NGAY
          PUSH 4
          PUSH 5
          READ2_BCD MONTH
          MOV A,R4
          MOV R0,A          ;R0 LUU GIA TRI THANG
          MOV A,R5
          MOV R7,A          ;R7 LUU GIA TRI NAM
          POP 5
          POP 4
    
    HOW_MANY_DAY T0_M3,T0_31DAY,T0_M5,T0_M7,T0_M8,T0_M10,T0_M12,T0_M4,T0_M6,T0_30DAY,T0_M9,T0_M11,T0_M2,T0_M2_28DAY,T0_M2_29DAY
    T0_30DAY: CJNE R5,#1,DEC_R5
              MOV R5,#30
              LJMP T0_END
    T0_31DAY: CJNE R5,#1,DEC_R5
              MOV R5,#31
              LJMP T0_END
    T0_M2_28DAY: CJNE R5,#1,DEC_R5
                 MOV R5,#28
                 LJMP T0_END
    T0_M2_29DAY: CJNE R5,#1,DEC_R5
                 MOV R5,#29
                 LJMP T0_END

    T0_8: CMP R3,11,D_RED2,D_MIN
    D_RED2: CJNE R5,#16,DEC_RED2        ;GIAM THOI GIAN DEN DO CUA COT DEN 2
            LJMP T0_END
    DEC_RED2: MOV A,R5
              CLR C
              SUBB A,#2
              MOV R5,A
              LJMP T0_END
    D_MIN: CJNE R5,#0,DEC_R5
           MOV R5,#59
           LJMP T0_END
    DEC_R5: DEC R5
            LJMP T0_END

    T0_BLINKMIN: JNB BLINKMIN,T0_BLINKHOUR
                 CMP R3,71,T0_BLINKMIN_1,D_HOUR
    T0_BLINKMIN_1: CJNE R3,#22,T0_9
                   CJNE R4,#1,DEC_R4       ;GIAM CHI SO THANG
                   MOV R4,#12
                   LJMP T0_END

    T0_9: CJNE R3,#21,T0_10              ;GIAM CHI SO THU TRONG TUAN
          CJNE R4,#1,DEC_R4
          MOV R4,#7
          LJMP T0_END

    T0_10: CMP R3,11,D_RED1,D_HOUR
    D_RED1: CJNE R4,#16,DEC_RED1
            LJMP T0_END
    DEC_RED1: MOV A,R4
              CLR C
              SUBB A,#2
              MOV R4,A
              LJMP T0_END
    D_HOUR: CJNE R4,#0,DEC_R4
            MOV R4,#23
            LJMP T0_END
    DEC_R4: DEC R4
            LJMP T0_END
    
    T0_BLINKHOUR: CJNE R3,#0,T0_MONTH
                  CJNE R4,#16,T0_H3
                  LJMP T0_R1
    T0_H3: CJNE R5,#16,T0_H4
           LJMP T0_R1
    T0_H4: MOV RED1_TEMP,R4
           MOV RED2_TEMP,R5
           SETB IN_MOD4
           SETB MODC
           MOV MODD,#4
    T0_R1: READ2_BCD MONTH
           MOV R3,#22
           LJMP T0_END

    T0_MONTH: CJNE R3,#22,T0_DAY
              JNB MONTH_UPDATE,T0_MONTH_1
              WRITE2_BCD MONTH
              CLR MONTH_UPDATE
    T0_MONTH_1: MOV R3,#21
                READ2_BCD DAY
                LJMP T0_END

    T0_DAY: CJNE R3,#21,T0_84
            JNB DAY_UPDATE,T0_DAY_1
            WRITE2_BCD DAY
            CLR DAY_UPDATE
    T0_DAY_1: MOV R3,#84
              READ2 SUN2_POST
              LJMP T0_END

    T0_84: BUTTON 84,T0_83,SUN2_POST_UPDATE,LABEL03,SUN2_POST,83,SUN2_PRE,T0_END
    T0_83: BUTTON 83,T0_82,SUN2_PRE_UPDATE,LABEL04,SUN2_PRE,82,SUN1_POST,T0_END
    T0_82: BUTTON 82,T0_81,SUN1_POST_UPDATE,LABEL05,SUN1_POST,81,SUN1_PRE,T0_END
    T0_81: BUTTON 81,T0_74,SUN1_PRE_UPDATE,LABEL06,SUN1_PRE,74,SAT2_POST,T0_END
    T0_74: BUTTON 74,T0_73,SAT2_POST_UPDATE,LABEL07,SAT2_POST,73,SAT2_PRE,T0_END
    T0_73: BUTTON 73,T0_72,SAT2_PRE_UPDATE,LABEL08,SAT2_PRE,72,SAT1_POST,T0_END
    T0_72: BUTTON 72,T0_71,SAT1_POST_UPDATE,LABEL09,SAT1_POST,71,SAT1_PRE,T0_END
    T0_71: BUTTON 71,T0_14,SAT1_PRE_UPDATE,LABEL010,SAT1_PRE,14,TIME2_POST,T0_END
    T0_14: BUTTON 14,T0_13,TIME2_POST_UPDATE,LABEL011,TIME2_POST,13,TIME2_PRE,T0_END
    T0_13: BUTTON 13,T0_12,TIME2_PRE_UPDATE,LABEL012,TIME2_PRE,12,TIME1_POST,T0_END
    T0_12: BUTTON 12,T0_11,TIME1_POST_UPDATE,LABEL013,TIME1_POST,11,TIME1_PRE,T0_END
    T0_11: BUTTON 11,T0_02,TIME1_PRE_UPDATE,LABEL014,TIME1_PRE,2,RED_PEAK,T0_END

    T0_02: CJNE R3,#2,T0_01
           CJNE R4,#16,T0_H7
           LJMP T0_R3
    T0_H7: CJNE R5,#16,T0_H8
           LJMP T0_R3
    T0_H8: WRITE2 RED_PEAK
    T0_R3: READ2 RED_NORMAL
           MOV R3,#1
           LJMP T0_END

    T0_01: CJNE R4,#16,T0_H5
           LJMP T0_R2
    T0_H5: CJNE R5,#16,T0_H6
           LJMP T0_R2
    T0_H6: WRITE2 RED_NORMAL
    T0_R2: MOV R3,#0
           MOV R4,#16
           MOV R5,#16
           LJMP T0_END
T0_END: RETI
 
LED7: DB 0C0H,0F9H,0A4H,0B0H,99H,92H,82H,0F8H,80H,90H
END

        
